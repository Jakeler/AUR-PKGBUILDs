# Maintainer: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>
# Maintainer: Cody Schafer <aur@codyps.com>
# Contributor: Jake <aur@ja-ke.tech>

pkgname=dsview
pkgver=0.99
pkgrel=1
pkgdesc="Client software that supports the DreamSourceLab logic analyzer"
arch=('i686' 'x86_64')
url="http://www.dreamsourcelab.com/"
license=('GPL3')
depends=('boost-libs' 'qt5-base' 'libsigrokdecode4dsl' 'libsigrok4dsl' 'fftw')
makedepends=('boost')

source=("https://github.com/DreamSourceLab/DSView/archive/${pkgver}.tar.gz"
        'udev.rules'
        'dsview.desktop')
sha512sums=('bb773a86757ea74442c73e6068398e9d39455fbbda578af1eccf405ad52063391e698301e9f6393462621e0f412a598ccecaceaa152cb696111ed42b79501998'
            '64de76cfa9ed23de2a0ae81219742f8660a82d5b89bc16b903b22b57f786cb69b6c249f972f467ff387675f3217cba6af26908e0819c282688e46310fd5b0922'
            '6004b6a2be8185bf3a7962135c8eb557767a578b67d500cd17502c62a844a61ee531974a8ab9203943ddf439c2dd67a4b97dcec960cffb4253aca3d0f949fc85')

prepare() {
  cd "$srcdir/DSView-${pkgver}/DSView"
  sed -i 's#install(FILES icons/logo.png DESTINATION share/${PROJECT_NAME} RENAME logo.png)##; 
          s#install(FILES DreamSourceLab.rules DESTINATION /etc/udev/rules.d/)##; 
          s#install(FILES DSView.desktop DESTINATION /usr/share/applications/)##' CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_SKIP_RPATH=1 .
}

build() {
  cd "$srcdir/DSView-${pkgver}/DSView"
  cmake .
  make
}

package() {
  cd "$srcdir/DSView-${pkgver}/DSView"

  make DESTDIR="$pkgdir" PREFIX=/usr install
  
  mv "$pkgdir/usr/bin/DSView" "$pkgdir/usr/bin/${pkgname}"
  
  install -Dm644 "$srcdir/dsview.desktop" "$pkgdir/usr/share/applications/dsview.desktop"
  install -Dm644 "$srcdir/udev.rules" "$pkgdir/usr/lib/udev/rules.d/20-dsview.rules"
  
  for i in 16 32 48 64 128 256; do
    install -Dm644 icons/logo_${i}.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
  done
}

# vim:set ts=2 sw=2 et:
